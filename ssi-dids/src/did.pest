// DID Scheme https://w3c.github.io/did-core/#did-syntax
// (accessed 20200711)
//
// did                = "did:" method-name ":" method-specific-id
// method-name        = 1*method-char
// method-char        = %x61-7A / DIGIT
// method-specific-id = *( *idchar ":" ) 1*idchar
// idchar             = ALPHA / DIGIT / "." / "-" / "_"
did_scheme = { "did:" ~ method_name ~ ":" ~ method_specific_id }
method_name = { method_char+ }
method_char = _{ ASCII_ALPHA_LOWER | ASCII_DIGIT }
method_specific_id = { ( idchar* ~ ":" )* ~ idchar* }
idchar = _{ ASCII_ALPHANUMERIC | "." | "-" | "_" }

// DID URL https://w3c.github.io/did-core/#did-url-syntax
// (accessed 20200711)
// NOTE: The spec defines path-abempty, fragment, and pchar as those from
// RFC3986 (URI: Generic Syntax) so we have included the relevant constructions
// in the section below. 
//
// did-url = did path-abempty [ "?" query ] [ "#" fragment ]
did_url = { did_scheme ~ path_abempty ~ ( "?" ~ query ) ~ ( "#" ~ fragment )? }

// RFC3986 https://tools.ietf.org/html/rfc3986
// (accessed 20200711, relevant constructions only)
//
// path-abempty  = *( "/" segment )
// segment       = *pchar
// query         = *( pchar / "/" / "?" )
// fragment      = *( pchar / "/" / "?" )
// pchar         = unreserved / pct-encoded / sub-delims / ":" / "@"
// pct-encoded   = "%" HEXDIG HEXDIG
// unreserved    = ALPHA / DIGIT / "-" / "." / "_" / "~"
// reserved      = gen-delims / sub-delims
// gen-delims    = ":" / "/" / "?" / "#" / "[" / "]" / "@"
// sub-delims    = "!" / "$" / "&" / "'" / "(" / ")"
//               / "*" / "+" / "," / ";" / "="
path_abempty = { ( "/" ~ segment )* }
segment = { pchar* }
query = { ( pchar | "/" | "?" )* }
fragment = { ( pchar | "/" | "?" )* }
pchar = _{ unreserved | pct_encoded | sub_delims | ":" | "@" }
pct_encoded = _{ "%" ~ ASCII_HEX_DIGIT ~ ASCII_HEX_DIGIT }
unreserved = _{ ASCII_ALPHA | ASCII_DIGIT | "-" | "." | "_" | "~" }
reserved = _{ gen_delims | sub_delims }
gen_delims = _{ ":" | "/" | "?" | "#" | "[" | "]" | "@" }
sub_delims = _{ "!" | "$" | "&" | "'" | "(" | ")"
              | "*" | "+" | "," | ";" | "=" }  

